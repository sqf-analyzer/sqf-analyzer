WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    =  { ("//" ~ (!"\n" ~ ANY)*) | ("/*" ~ (!"*/" ~ ANY)* ~ "*/") }

program = _{ SOI ~ term* ~ EOI }

term = _{ include | undef | define | if_ | base_term }

string    = @{ "\"" ~ ("\"\"" | (!"\"" ~ ANY))* ~ "\"" }
number    = @{ "-"? ~ ASCII_DIGIT ~ (ASCII_DIGIT | ".")* }
word      = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
symbol    =  { "&&" | "||" | "==" | "<=" | ">=" | "!=" | SYMBOL | PUNCTUATION | NUMBER }
base_term = _{ number | string | word | symbol }

// #include
include     = ${ "#include" ~ " " ~ include_str ~ end }
include_str = @{ ("<" ~ (!">" ~ ANY)* ~ ">") | "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

// #undef
undef = ${ "#undef" ~ " " ~ word ~ end }

// #if | #ifdef | #ifndef
if_ = {
    (ifdef_start | if_start) ~ if_body ~ (else_ ~ if_body)? ~ "#endif"
}

if_term     = _{ (!("#endif" | else_) ~ term) }
if_body     =  { if_term* }
else_       =  { "#else" }
ifdef_start = ${ if_def_kw ~ " " ~ word ~ "\n" }
if_def_kw   =  { "#ifdef" | "#ifndef" }

// #if
if_start = ${ "#if" ~ " " ~ if_expr ~ "\n" }
if_expr  =  { (word | number) ~ (" "* ~ if_op ~ " "* ~ (word | number))? }
if_op    = @{ "<=" | ">=" | "<" | ">" | "==" | "!=" }

// #define
define = ${
    "#define" ~ " " ~ word ~ define_arguments? ~ " "* ~ define_term* ~ end
}

define_arguments =  { "(" ~ word ~ ("," ~ word)* ~ ")" }
define_term      = @{ "\\\n" | "##" | "#" | base_term | " " | "\t" | "\r" }

end = _{ ("\n" | EOI) }
