WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    =  { ("//" ~ (!"\n" ~ ANY)*) | ("/*" ~ (!"*/" ~ ANY)* ~ "*/"?) }

program = _{ SOI ~ "\u{feff}"? ~ term* ~ EOI }

term = _{ include | undef | define | if_ | base_term }

string = @{ ("\"" ~ ("\"\"" | (!"\"" ~ ANY))* ~ "\"") | ("'" ~ ("''" | (!"'" ~ ANY))* ~ "'") }

// SQF number regex: (((\$|0x)[0-9a-fA-F]+)|(\.[0-9]+))|(\b[0-9]+(\.[0-9]+|[eE][-+]?[0-9]+)?)\b
// from https://community.bistudio.com/wiki/Number
number    = @{
    ("0x" ~ ASCII_ALPHANUMERIC+) | // (\$|0x)[0-9a-fA-F]+
    ("." ~ ASCII_DIGIT+) | // \.[0-9]+
    (ASCII_DIGIT+) ~  // \b[0-9]+
    ("." ~ ASCII_DIGIT*)? ~
    (("e" | "E") ~ ("+" | "-")? ~ ASCII_DIGIT+)? // [eE][-+]?[0-9]+
}
word      = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* | NUMBER ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
symbol    =  { "&&" | "||" | "==" | "<=" | ">=" | "!=" | "<<" | ">>" | SYMBOL | PUNCTUATION | NUMBER }
base_term =  { string | word | number | symbol }

// #include
include_kw        =  { "#include" }
include           = ${ include_kw ~ " " ~ include_str ~ " "* ~ (end | COMMENT) }
include_str_inner = @{ (!("\"" | ">") ~ ANY)* }
include_str       = _{ ("<" ~ include_str_inner ~ ">") | "\"" ~ include_str_inner ~ "\"" }

// #undef
undef_kw =  { "#undef" }
undef    = ${ undef_kw ~ " " ~ word ~ end }

// #if | #ifdef | #ifndef
if_ = ${
    (ifdef_start | if_start) ~ if_body ~ (else_ ~ end ~ if_body)? ~ endif_kw ~ end
}

if_term     = _{ (!(endif_kw | else_) ~ term) }
if_body     =  { (WHITESPACE | COMMENT | if_term)* }
else_       =  { "#else" }
ifdef_start = ${ ifdef_kw ~ " "+ ~ word ~ end }
ifdef_kw    =  { "#ifdef" | "#ifndef" }
endif_kw    =  { "#endif" }

// #if
if_start   = ${ if_keyword ~ " "* ~ base_term* ~ " "* ~ end }
if_keyword =  { "#if" }

// #define
define = ${
    define_kw ~ " " ~ word ~ define_arguments? ~ " "* ~ (COMMENT | define_term)* ~ " "* ~ end
}

define_kw     =  { "#define" }
define_quote  =  { "#" }
define_quoted = ${ define_quote ~ word }
define_concat =  { "##" }

define_arguments =  { "(" ~ " "* ~ (word ~ (" "* ~ "," ~ " "* ~ word)*)? ~ " "* ~ ")" }
define_term      = _{ define_quoted | define_concat | "\\\n" | base_term | " " | "\t" | "\r" }

end = _{ ("\r\n" | "\n" | EOI) }
