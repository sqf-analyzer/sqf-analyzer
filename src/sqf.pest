WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    = { "//" ~ (!"\n" ~ ANY)* }

define = ${
    define_keyword ~ " " ~ define_name ~ " "* ~ define_arguments? ~ " "* ~ define_body? ~ " "* ~ "\n"
}

define_keyword = { "#define" }
define_arguments = _{ "(" ~ define_argument ~ ("," ~ define_argument)* ~ ")" }
define_argument  = @{ (ASCII_ALPHA | "_") ~ (ASCII_DIGIT | ASCII_ALPHA | "_")* }
define_name      = @{ (ASCII_ALPHA | "_") ~ (ASCII_DIGIT | ASCII_ALPHA | "_")* }
define_body      = @{ (!"\n" ~ ANY)* }

include_keyword = { "#include" }
include = { include_keyword ~ string }
macro_content = @{ (!(")" | ",") ~ ANY)* }
macro_  = ${ variable ~ "(" ~ macro_content ~ ("," ~ macro_content)* ~ ")" }

program = _{ SOI ~ (define | include | (expr ~ ";"))* ~ (expr ~ ";"?)? ~ EOI }
expr    =  { assignment | primary+ }
primary = _{ macro_ | value | op | "(" ~ expr ~ ")" }

value = _{ array | string | number | boolean | variable | code }

number   = @{ "-"? ~ (ASCII_DIGIT | ".")+ }
string   = @{ "\"" ~ ("\"\"" | (!"\"" ~ ANY))* ~ "\"" }
boolean  = @{ "true" | "false" }
array    =  {
    "[" ~ "]"
  | "[" ~ expr ~ ("," ~ expr)* ~ "]"
}
code     =  {
    "{" ~ (expr ~ ";"?)* ~ "}"
}

variable = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* }

op = {
  "=="
  | "+"
  | "-"
  | "*"
  | "/"
  | "&&"
  | "&"
  | "!="
  | "<="
  | ">="
  | ">"
  | "<"
  | "||"
  | "^"
  | "!"
}

private_keyword = { "private" }
assignment = {
  private_keyword? ~ variable ~ "=" ~ expr
}
